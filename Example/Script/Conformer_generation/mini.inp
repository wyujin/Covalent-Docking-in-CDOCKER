* TITLE
* Created by Yujin Wu
* Minimize ligand random conformer 
* 2021/03/26
*
 
!-----------------------------------------------------------------------
! Prepare ligand
!-----------------------------------------------------------------------

!! Read the topology and parameter files for ligand 
bomlev -1
read rtf card name "../Toppar/top_all36_prot.rtf" 
read rtf card name "../Toppar/top_all36_cgenff.rtf" append
read param flex card name "../Toppar/par_all36m_prot.prm" 
read param flex card name "../Toppar/par_all36_cgenff.prm" append
bomlev 0

stream "ligandrtf"

!! Build ligand
read sequ pdb name "ligand.pdb"
bomlev -1
generate LIGA setup
auto angle dihe
bomlev 0
read coor pdb name "ligand.pdb" resid
write coor card select segid LIGA end name "ligand.crd"

!! Check if it has lone pair
!! If it has lone pair -- redistribute this charge
define lp select type lp* .and. segid LIGA end
if ?nsel .gt. 0 then
   set nlp = ?nsel
   scalar charge stat select .not. type lp* end
   calc qinc = -?stot / @nlp
   scalar charge add @qinc select type cl* .or. type br* .or. type i* end
   delete atom select lp end
   scalar charge stat
endif

!-----------------------------------------------------------------------
! Generate random ligand conformer
!-----------------------------------------------------------------------

!! Variables
set idx = 1
open unit 52 write form name "conformer.dat"

!! Generate random rotamer
label conformer
read coor pdb name "conformer/@IDX.pdb" resid

!-----------------------------------------------------------------------
! Calculate rmsd after minimization in vacuum 
!-----------------------------------------------------------------------

!! Brief minimization
update atom switch vswitch cutnb 12 ctofnb 10 ctonnb 8 rdie epsilon 3
mini sd nstep 50
mini abnr nstep 1000 tolenr 0.001

!! RMSD difference
read coor comp select segid LIGA end card name "ligand.crd" resid
coor orient rms select segid LIGA .and. (.not. type *H*) end
set dockrmsd = ?rms

!! Save result
write title unit 52
* @idx @dockrmsd ?ener 
*
write coor pdb select segid LIGA end name "optimized/@IDX.pdb"

!! Loop
if @idx .lt. @num then
   incr idx by 1
   goto conformer
   endif

stop

